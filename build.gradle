/*
 * Copyright 2021 Benjamin Martin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'io.codearte.nexus-staging' version '0.30.0'
}

group = 'net.lapismc'
archivesBaseName = "LapisDataStore"
version = '1.2.3'
description = 'An API for interfacing with SQL databases'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
    mavenLocal()
    //Spigot Repo
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/groups/public/')
    }
}

dependencies {
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'com.h2database:h2:2.1.214'
    implementation 'org.xerial:sqlite-jdbc:3.40.0.0'
    implementation 'org.slf4j:slf4j-nop:2.0.5'
    compileOnly 'org.spigotmc:spigot-api:1.19.3-R0.1-SNAPSHOT'
}

shadowJar {
    archiveFileName.set(archivesBaseName + ".jar")
    exclude 'javax/**'
    relocate("org.h2", "net.lapismc.datastore.drivers.h2")
    relocate("org.sqlite", "net.lapismc.datastore.drivers.sqlite")
    relocate("com.zaxxer", "net.lapismc.datastore.drivers")
    relocate("org.slf4j", "net.lapismc.datastore.drivers.hikari.slf4j")
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('copyToCompiled', Copy) {
    from layout.buildDirectory.dir("libs/LapisDataStore.jar")
    into layout.projectDirectory.dir("compiled")
}


artifacts {
    archives javadocJar, sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

nexusStaging {
    packageGroup = "net.lapismc" //optional if packageGroup == project.getGroup()
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name = project.archivesBaseName
                packaging 'jar'
                // optionally artifactId can be defined here
                description = project.description
                url 'https://github.com/Dart2112/LapisDataStore'

                scm {
                    connection 'git@github.com:Dart2112/LapisDatastore.git'
                    developerConnection 'git@github.com:Dart2112/LapisDatastore.git'
                    url 'https://github.com/Dart2112/LapisDatastore'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'Dart2112'
                        name 'Benjamin Martin'
                        email 'dart2112@lapismc.net'
                    }
                }
            }
        }
    }
}